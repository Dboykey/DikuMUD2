/*
filename    deadl1
password    zombie
changedby   Jager
EmailAdd     Palio <palio@blinksoft.com>
request     compile
version     8
END HEADER*/

#include <composed.h>

%zone deadl1
title "Death Swamps"
lifespan 40
reset RESET_ANYHOW
creators {"thesar"}

notes
"
Update 01 Jan 97 -- by Jager&n

I added another clan link (see map below)

Update 08 Nov 96 -- by Jager&n

I added a clan link (see map below)

Update 25 Mar 95 -- by Thesar&n&n

This zone was written by someone else.  Due to the many missing room
links and the need to update the zone to comply with new guidelines,
I have changed some parts.  Also fixed some descriptions and typos.&n&n

New Map:
&l
                             ni3 -- ni2           co3
                                     |             |
                                    ni1 -- co1 -- co2
                                     |
fall -- rv2 -- rv1 -- rv3 -- rv4 -- b_i -- rv5(wfall)
                |                    |
              from                  so1 -- brd -- gdn
               d11                                 |
                                                  mdw
(clan home)
 |
tmb                    (to map above)
 |                           |
p07    p05 -- d09 -- d10 -- d11 (shrubbery)
 |      |      |      |
p01 -- p03    p06    d12 -- d14 -- d13 -- to death keep
 |      |                    |      |
p02 -- p04                  d15    d16 -- d17
                             |
                     d03 -- d04 -- d06 -- d07 - b_mtn
                      |             |
              d01 -- d02           d18
               |      |
             moor    d05 -- d08
               |             |
         (to midgaard)  (clan home)
"
help
"
Death Lands&n
-----------&n
The death swamps are for levels 3-5, the tomb for 6-10, the Galen
Island for level 14-19 and the Cabomba Island for levels 5-10.  To
get to the tomb and the islands you will need to sail by boat.&n&n

If there are any bugs or suggestions please mudmail Thesar.&n
"
%rooms

/*-----------------SWAMP ROOMS ------------------------*/

                de_swamp1

names {"death swamp"}
title "The Death Swamps"
descr
"   You standing in the death swamps.  The thick moisty air makes breathing
difficult, and the disgusting smell of death lingers around the air.  Some
mangrove trees thrives on the shallow marsh.  The death swamps continues
east of you.  A road to your south leads to the moor."

extra {"mangrove trees", "mangrove tree", "trees", "tree"}
"They have grown quite large, evidently old.  Huge sturdy roots
grows above the ground, tripping the unwary travellers."
extra {"marsh"}
"They are shallow but murky.  You can see small leeches occassionally
floating to the top."
movement SECT_SWAMP
south to moor/north_rd7;
east to de_swamp2;
end

                de_swamp2

title "The Death Swamps"
descr
"   You standing in the death swamps.  The thick moisty air makes breathing
difficult.  This ground is full of quagmires and small pools of mudding
water.  The swamps continue in north, south and west directions."

extra {"quagmire", "quagmires"}
"They are soft spots on the ground which you sink into when you try to
walk on them."
extra {"pools", "pool", "water"}
"The water is murky and gives of a foul smell."
movement SECT_SWAMP
north to de_swamp3;
south to de_swamp5;
west to de_swamp1;
end

                de_swamp3

title "The Death Swamps"
descr
"   You are in the death swamps.  The thick moisty air makes breathing
difficult.  Around you are quagmires and small pool.  There are some
small bushes growing here despite of the unfavourable conditions.
The wet and soft ground slows down movement, as if telling you to
watch your every step.  The swamps continue east and south."

extra {"quagmire", "quagmires"}
"They are soft spots on the ground which you sink into when you try to
walk on them."
extra {"pools", "pool", "water"}
"The water is murky and gives of a foul smell."
extra {"small bushes", "bushes"}
"They do not seem to be growing healthily.  Looks like they will
wither away soon."

movement SECT_SWAMP
east to de_swamp4;
south to de_swamp2;
end

                de_swamp4

title "The Death Swamps"
descr
"   You are in the death swamps. The thick moisty makes breathing
difficult.  Around you are quagmires and small pools.  The wet and
soft ground makes walking difficult, as if telling you to watch
your every step.  The swamp extends to your north and west.  You
also see a willow tree to your east."

extra {"quagmire", "quagmires"}
"They are soft spots on the ground which you sink into when you try to
walk on them."
extra {"pools", "pool", "water"}
"The water is murky and gives of a foul smell."

movement SECT_SWAMP
east to de_swamp6;
west to de_swamp3;
north to de_swamp15;
end

               de_swamp5

title "The Death Swamps"
descr
"   You are in the death swamps. The thick moisty makes breathing
difficult.  Around you are quagmires and small pools.  The wet and
soft ground makes walking difficult, as if telling you to watch
your every step.  The swamp extends to your north and east."

extra {"quagmire", "quagmires"}
"They are soft spots on the ground which you sink into when you try to
walk on them."
extra {"pools", "pool", "water"}
"The water is murky and gives of a foul smell."

movement SECT_SWAMP
east to de_swamp8;
north to de_swamp2;
end

               de_swamp6

title "At the Willow Tree in the Death Swamps"
descr
"   You are standing by a willow tree. The air is somewhat misty, but
breathing has become much easier here.  Small pools of murky water
surrounds the spot where the tree stands.  You may continue east,
west or south."

extra {"willow", "tree"}
"The willow tree is large, with long branches and narrow leaves
hanging down towards the ground."
extra {"pools", "water", "murky water"}
"The water in the pools emanates a slight, foul smell."
extra {"mist"}
"The mistiness around the tree seems strange, but you do not seem
to figure out what is wrong with it.  It surely gives you an errie
feeling."

movement SECT_SWAMP

east to de_swamp7;
west to de_swamp4;
south to de_swamp18;
end

               de_swamp7

title "The Death Swamps"
descr
"   You are in the death swamps. The thick moisty makes breathing
difficult.  Around you are quagmires and small pools.  The wet and
soft ground makes walking difficult, as if telling you to watch
your every step.  The swamps continue to the north and west.  To
your east the swamp clears into a foothill."

extra {"quagmire", "quagmires"}
"They are soft spots on the ground which you sink into when you try to
walk on them."
extra {"pools", "pool", "water"}
"The water is murky and gives of a foul smell."
extra {"foothill"}
"From here you can see nothing much about the foothill except that it
is rocky.  You may want to go east for a closer look."

movement SECT_SWAMP

east to base_mount;
north to de_swamp17;
west to de_swamp6;
end

               de_swamp8

title "The Death Swamps"
descr
"  You are in the death swamps.  The thick moisty air is becoming
increasingly difficult to breathe.  There is an acrid smell hanging
in the air.  Some mangrove trees grow closely on the shallow marsh.
On the ground is a half-sunken rotting log covered with moss.  The
only way leads back west.  There is dense fog to the south."

extra {"mangrove trees", "mangrove tree", "trees", "tree"}
"They have grown quite large, evidently old.  Huge sturdy roots
grows above the ground, tripping the unwary travellers."
extra {"marsh"}
"They are shallow but murky.  You can see small leeches occassionally
floating to the top."
extra {"rotting log", "log"}
"The remains of what was a mangrove tree.  Probably it fell pray
to some nasty insects."

movement SECT_SWAMP
west to de_swamp5;

SECRET_DOOR_DIFFICULTY(SOUTH, 100)
south to clan_home_014@clans
keyword {"fog"} open {EX_OPEN_CLOSE, EX_CLOSED, EX_HIDDEN} descr
"Something funny...";

end

               de_swamp9

title "The Northern Death Swamps"
descr
"   You are in the northern part of the death swamps.  The thick moisty air
makes it extremely hard to breath.  Around you are quagmires and small
pools.  Some small bushes grows here.  The swamp continues to the east.
To your south and west is a mere."

extra {"quagmire", "quagmires"}
"They are soft spots on the ground which you sink into when you try to
walk on them."
extra {"pools", "pool", "water"}
"The water is murky and gives of a foul smell."
extra {"small bushes", "bushes"}
"They do not seem to be growing healthily.  Looks like they will
wither away soon."

movement SECT_SWAMP

east to de_swamp10;
south to po_swamp6;
west to po_swamp5;
end

               de_swamp10

title "The Northern Death Swamps"
descr
"   You are in the northern part of the death swamps.  The thick moisty air
makes it extremely hard to breath.  Around you are quagmires and small
pools.  Some small bushes grows here.  The swamp continues to the east.
west and south."

extra {"quagmire", "quagmires"}
"They are soft spots on the ground which you sink into when you try to
walk on them."
extra {"pools", "pool", "water"}
"The water is murky and gives of a foul smell."
extra {"small bushes", "bushes"}
"They do not seem to be growing healthily.  Looks like they will
wither away soon."

movement SECT_SWAMP

east to de_swamp11;
south to de_swamp12;
west to de_swamp9;
end

               de_swamp11

title "The Northern Death Swamps"
descr
"   You are in the northern part of the death swamps.  The thick moisty air
makes it extremely hard to breath.  Around you are quagmires and small
pools.  A shrubbery grows there, accompanied by the sound of water flowing
in a river.  The only way is west back where you came from."

extra {"quagmire", "quagmires"}
"They are soft spots on the ground which you sink into when you try to
walk on them."
extra {"pools", "pool", "water"}
"The water is murky and gives of a foul smell."
extra {"shrubbery"}
"You seem to notice a small opening that leads into the shrubbery."

movement SECT_SWAMP

west to de_swamp10;
/* the shrubbery is placed inside this room */
end

               shrubbery

names {"shrubbery", "shrub"}
title "Inside the Shrubbery"
descr
"   You are inside a shrubbery.  The leaves and branches block your
view and make movement difficult.  To your north you can barely make
out a small river flowing in the east-west direction."

extra {"leaves", "branches"}
"The are part of the shrubbery."
extra {"river"}
"You cannot see anymore clearly from here."

manipulate {MANIPULATE_ENTER}
movement SECT_FOREST
in de_swamp11
north to riv_swamp1;
end

               de_swamp12

title "The Northern Death Swamps"
descr
"   You are in the northern part of the death swamps.  The thick moisty air
makes it extremely hard to breath.  Around you are quagmires and small
pools.  Some small bushes grows here.  The swamp continues to the north
and east."

extra {"quagmire", "quagmires"}
"They are soft spots on the ground which you sink into when you try to
walk on them."
extra {"pools", "pool", "water"}
"The water is murky and gives of a foul smell."
extra {"small bushes", "bushes"}
"They do not seem to be growing healthily.  Looks like they will
wither away soon."

movement SECT_SWAMP

north to de_swamp10;
east to de_swamp14;
end

               de_swamp13

title "The Northern Death Swamps"
descr
"   You are in the northern part of the death swamps.  The thick moisty air
makes it extremely hard to breath.  Around you are quagmires and small
pools.  Some small bushes grows here.  An old run-down building stands to
the far east.  The swamp continues to the west and south."

extra {"quagmire", "quagmires"}
"They are soft spots on the ground which you sink into when you try to
walk on them."
extra {"pools", "pool", "water"}
"The water is murky and gives of a foul smell."
extra {"small bushes", "bushes"}
"They do not seem to be growing healthily.  Looks like they will
wither away soon."

movement SECT_SWAMP

south to de_swamp16;
west to de_swamp14;
east to keep_entrance@keep;
end

               de_swamp14

title "The Middle Death Swamps"
descr
"   You are in the middle part of the death swamps.  The thick moisty air
makes breathing difficult.  This part of the swamps is covered with
a shallow marsh.  You may continue to your east, west or south."

extra {"marsh"}
"They are shallow but murky.  You can see small leeches occassionally
floating to the top."

movement SECT_SWAMP
south to de_swamp15;
east to de_swamp13;
west to de_swamp12;
end

               de_swamp15

title "The Middle Death Swamps"
descr
"   You are in the middle part of the death swamps.  The thick moisty air
makes breathing difficult.  This part of the swamps is covered with
a shallow marsh.  You are hit by a strong, pungent smell in this place.
The swamps extends north and south."

extra {"marsh"}
"They are shallow but murky.  You can see small leeches occassionally
floating to the top."

movement SECT_SWAMP

south to de_swamp4;
north to de_swamp14;
end

              de_swamp16

title "The Middle Death Swamps"
descr
"   You are in the middle part of the death swamps.  The thick moisty air
makes breathing difficult.  This part of the swamps is covered with
a shallow marsh.  You are hit by a strong, pungent smell in this place.
The swamps extends north and east."

extra {"marsh"}
"They are shallow but murky.  You can see small leeches occassionally
floating to the top."

movement SECT_SWAMP

north to de_swamp13;
east to de_swamp17;
end

              de_swamp17

title "The Eastern Death Swamps"
descr
"   You are in the eastern part of the death swamps.  The thick moisty
air makes breathing difficult.  On the marshy grounds you see a small
pile of bones remaining from an unfortunate victim."

extra {"bone", "bones"}
"They they seem to be remains of a humaniod creature."
extra {"marsh", "marshy ground"}
"They are shallow but murky.  You can see small leeches occassionally
floating to the top."

movement SECT_SWAMP

west to de_swamp16;
south to de_swamp7;
end

              de_swamp18

title "The Death Swamps"
descr
"   You are in the death swamps. The thick moisty makes breathing
difficult.  Around you are quagmires and small pools.  The wet and
soft ground makes walking difficult, as if telling you to watch
your every step.  The only way to back north."

extra {"quagmire", "quagmires"}
"They are soft spots on the ground which you sink into when you try to
walk on them."
extra {"pools", "pool", "water"}
"The water is murky and gives of a foul smell."

movement SECT_SWAMP

north to de_swamp6;
end

              po_swamp1

title "The Death Mere"
descr
"   You are on the death mere, a thick mist hangs in the air."

movement SECT_WATER_SAIL
south to po_swamp2;
east to po_swamp3;
north to po_swamp7;
end

              po_swamp2

title "The Death Mere"
descr
"   You are on the death mere, a thick mist flows in the air.  A
feeling of death and loneliness seems to sweep this place."

movement SECT_WATER_SAIL
north to po_swamp1;
east to po_swamp4;
end

              po_swamp3

title "The Death Mere"
descr
"   You are on the death mere, a thick mist flows in the air."

movement SECT_WATER_SAIL
east to po_swamp6;
west to po_swamp1;
north to po_swamp5;
end

              po_swamp4

title "The Death Mere"
descr
"   You are on the death mere, a thick mist flows in the air. You
can hear the creeking sound of something."

movement SECT_WATER_SAIL
west to po_swamp2;
end

              po_swamp5

title "The Death Mere"
descr
"   You are on the death mere, a thick mist flows in the air."

movement SECT_WATER_SAIL
east to de_swamp9;
south to po_swamp3;
end

              po_swamp6

title "The Death Mere"
descr
"       You are on the death mere, a transparent mist lies over
the water. You can discern land to the north and to the south."
movement SECT_WATER_SAIL
north to de_swamp9;
west to po_swamp3;
end

              po_swamp7

names {"death mere"}
title "the Death Mere"
descr
"       You are on the death mere, a transparent mist lies over
the water. You can discern land to the north."
movement SECT_WATER_SAIL
south to po_swamp1;
north to isl_swamp;
end

              isl_swamp

title "The Tomb Island"
descr
"   You are standing on a very small island.  A large tombstone, covered
with moss, is mounted over the tomb.  You see an entrance leading into
the tomb.  You may also leave the island by sailing south."

extra {"tombstone"}
"It has some engravings on it, probably an ancient language."
extra {"tomb"}
"The tomb is nearly covered with and moss."

movement SECT_FIELD

south to po_swamp7;
/* The tomb is placed here */
end

              tomb

names {"tomb"}
title "Inside the Tomb"
descr
"   You are inside the ancient tomb.  The tomb appears to be carved
out from a single piece of rock, but even then the walls are surprisingly
smooth.  On the east wall is a sculpture of a large human head.  You
also see strange but beautifully carvings on the ceiling."

extra {"walls", "wall"}
"The walls are very smooth, sign of an excellent craftsmanship."
extra {"head"}
"It is made of bronze.  His mouth is gaping tremendously wide, seemingly
able to swallow a person alive."
flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS}
manipulate {MANIPULATE_ENTER}
movement SECT_INSIDE
/* head is placed here */
in isl_swamp
up to isl_swamp;

SECRET_DOOR_DIFFICULTY(DOWN, 100)
down to clan_home_024@clans
keyword {"trapdoor", "floor"} open {EX_OPEN_CLOSE, EX_CLOSED, EX_HIDDEN} descr
"Something funny...";

end

              head

names {"gape","mouth","head"}
title "Inside the Mouth"
descr
"   You are inside the mouth of a large bronze head.  A ladder
leads down the throat."

extra {"ladder"}
"It looks old but still usable."

flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS}
manipulate {MANIPULATE_ENTER}
movement SECT_INSIDE
down to crypt;
in tomb
end

              crypt

title "In the Crypt"
descr
"   You are inside the secret crypt.  The air smells awfully musty here.
Water leaks from the ceiling down the walls and slowly collects on the
ground."

flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS}
manipulate {MANIPULATE_ENTER}
movement SECT_INSIDE
up to head;
end

              riv_swamp1

title "The Meandering River"
descr
"   You are on the meandering river. The stream is so strong that you
hardly can manage to pull yourself up stream. The river continues
upstream to the east and downstream to the west."

movement SECT_WATER_SAIL
west to riv_swamp2;
east to riv_swamp3;
south to shrubbery;
end

              riv_swamp2

title "The Meandering River"
descr
"   You are on the meandering river. The stream is so strong that you can
hardly manage to pull yourself upstream.  You hear the splashing sound of
the meandering falls downstream to your west.  To the east the river
continues upstream."

movement SECT_WATER_SAIL
east to riv_swamp1;
west to riv_falls;
special SFUN_FORCE_MOVE "deadl1/riv_falls!The strong current washes you "+
                        "downstream!" time 30 bits SFB_RANTIME
end

              riv_falls

title "The Meandering Falls"
descr
"   You splash down the falls hitting on the jutting rocks protruding
by the side of the falls.  You land in the deep gorge and water continues
to splash on you injuring you due to the impact.  You had better get
out of this place fast!"

extra {"rocks", "rock"}
"Although the rocks are smoothen through centuries of water erosion,
the are nevertheless hard and deadly-looking."
extra {"gorge", "water"}
"The gorge is deep, and with water continuing to splash on your head
you will drown soon!"

movement SECT_WATER_SAIL
flags {UNIT_FL_NO_MOB}
special SFUN_DEATH_ROOM "5" time 15
special SFUN_FORCE_MOVE "deadl1/fall!The rushing water flushes you to a" +
                        "new place!@$1n is flushed away by the rushing water!"
                        time 120 bits SFB_RANTIME
end

              riv_swamp3

title "The Meandering River"
descr
"   You are on the meandering river.  The current is not so strong here
making movement easier.  The river flows along the east-west direction."

movement SECT_WATER_SAIL
west to riv_swamp1;
east to riv_swamp4;
special SFUN_FORCE_MOVE "deadl1/riv_swamp1!You drift downstream to your "+
                        "west." time 70 bits SFB_RANTIME
end

              riv_swamp4

title "The Meandering River"
descr
"   You are on the meandering river.  Here you see rows of closely packed
mangrove trees growing on either side of the river bank.  To your east
the river widens out to a lake.  The river continues to flow in the
east-west directions."

extra {"lake"}
"The lake seems to be quite big."
extra {"mangrove", "trees", "tree"}
"Those trees certainly love wet and swampy areas."

movement SECT_WATER_SAIL
west to riv_swamp3;
east to betw_isl;
end

              betw_isl

title "On a Lake"
descr
"   You are travelling on a lake.  The water current is hardly noticeable
here.  North of you is a small island with a cottage.  To your south you
see an even smaller island with some peculiar plants growing on it.  You
can also travel east-west along the meandering river."

extra {"island", "islands"}
"There are two islands north and south of here."
extra {"cottage"}
"Probably owned by some human inhabitants."
extra {"plants"}
"Those plants look strange.  You need a closer look to find out."

movement SECT_WATER_SAIL
west to riv_swamp4;
east to riv_swamp5;
north to no_isl1;
south to so_isl1;
end

              riv_swamp5

title "At the Waterfall"
descr
"   You are standing in front of a waterfall.  Quick-flowing water rushes
off the cliff splashing down the gorge forming a beautiful sight.  You
can also see a rainbow arcing gracefully across the waterfall.  There is
no way to climb up the cliff, though you can see a clearing of some sort
through the falling waters on your east.  The other way is back west."

extra {"waterfall"}
"Clear water splashing down from the cliff-top.  It is indeed a
beautiful sight."
movement SECT_WATER_SAIL
east to fall;
west to betw_isl;
end

              fall

title "In the Waterfall"
descr
"   You are inside the waterfall.  Cold water flushes onto your body
giving you a refreshing feeling.  You can go west out of the waterfall
to the gorge."

movement SECT_WATER_SAIL
west to riv_swamp5;
end

/*--------------- GALEN ISLAND ----------------*/

             no_isl1

title "The Galen Island"
descr
"       You are on the bank of the northern island.  There is a small
brown cottage made of clay to your east.  Traces of thin smoke wavers
up into the air from the chimney.  North of you is a small meadow."

movement SECT_HILLS

north to no_isl2;
east to cottage1 open {EX_OPEN_CLOSE, EX_CLOSED}
	 keyword{"wooden door", "door"};
south to betw_isl;
end

             no_isl2

title "The Galen Island"
descr
"    You are on a small meadow on Galen island.  Healthy green carpet
grass covers the entire stretch of the meadow.  Some sunflowers grows
among the small bushes at one end.  You may continue west or south."

movement SECT_FIELD

west to no_isl3;
south to no_isl1;
end

             no_isl3

title "The Galen Island"
descr
"   You are on a small meadow on Galen island.  Healthy green carpet
grass covers the entire stretch of the meadow.  On the far end you see
birds chirping happily on the branches of trees."

movement SECT_FIELD

east to no_isl2;
end

             cottage1

title "Inside the Cottage"
descr
"       You are inside the small brown cottage.  There is something about
this place that makes it stink awfully.  The walls of the cottage are made
of clay, and they are totally devoid of any decorations."

movement SECT_INSIDE
flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS}
east to cottage2;
west to no_isl1 open {EX_OPEN_CLOSE, EX_CLOSED}
	 keyword{"door"};
end

             cottage2

title "The Kitchen"
descr
"   You are in the kitchen of the cottage.  There is nothing special
that you notice about this room, except that the awful smell seems
to evolve from here."

movement SECT_INSIDE
flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS}
north to cottage3;
west to cottage1;
end

             cottage3

title "The Bedroom"
descr
"   You are in the bedroom of the cottage.  A crudely made bed rests
here looking as dirty as ever.  The terrible smell is somewhat lesser
here, but still strong enough to make a horse puke."

extra {"bed"}
"It is bed from wood, probably by an unskilled person."

movement SECT_INSIDE
flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS}
south to cottage2;
end

/* ------------- SOUTHERN ISLAND ------------*/

            so_isl1

title "The Cabomba Island"
descr
"   You are on the Cabomba island.  The plants which are found here does
not look like anything you have seen before.  Some shrubberies of a strange
species grow here occupying at least half the ground.  You may continue
to your north or east."

extra {"plant", "shrubbery"}
"They look strange.  Long, pointed leaves of brownish colour which does
not distinguish themselves much from their stems."

movement SECT_FOREST

north to betw_isl;
east to breed_dom;
end

            breed_dom

title "The Breeders Domain"
descr
"   You are on the Cabomba island.  There are less shrubberies here, but
you notice there are a number of odd holes in the ground.  The path
continues to your east and west."

movement SECT_FIELD

east to garden;
west to so_isl1;
end

            garden

title "The Breeders Garden"
descr
"   You are in a garden of the Cabomba island.  The garden is extremely well
kept; you can hardly see any weeds around.  You can return to the breeders
domain to your west, or south to a meadow."

movement SECT_FIELD
flags {UNIT_FL_NO_TELEPORT}
south to meadow;
west to breed_dom;
end

            meadow

title "The Meadow"
descr
"   You are on a meadow in the Cabomba island.  There is a very beautiful
and relaxing feeling here, with birds chirping merry tunes of fortune
and butterflies darting from flower to flower.  The only way leads
back north to the garden."

extra {"birds", "bird"}
"They soar freely in the sky and chirping away."
extra {"butterfly", "butterflies"}
"They are small and beautifully coloured."

movement SECT_FIELD

north to garden;
end

/* --------- BASE OF MOUNTAIN ------------*/


            base_mount

title "The Base of the Mountain"
descr
"   You are at the base of the rocky mountains.  A small trail used to
wind up the side of the mountain.  Unfortunately some large pieces of
rocks now block the trail up, shutting of the route completely.  You
can only go back west into the swamps. You notice a small cave entrance
downwards."

extra {"trail"}
"It is a rocky trail winding up its way to the mountain top."
extra {"rock", "rocks"}
"They are large granite rocks, seemingly rolled down from above."
extra {"cave", "cavern","entrance"}
"It looks very deep and dark."

movement SECT_MOUNTAIN

west to de_swamp7;
down to center/cavern1;
end

%mobiles

/*------------------ DEADSWAMP MOBS ----------------------*/

			ho_zombie

names {"hollow zombie","zombie"}
title "a hollow zombie"
descr "A hollow zombie is wandering around the swamps."
extra {}
"A filthy creature with rotting flesh, it stares blankly at you."
M_ZOMBIE_HOLLOW
money 1 SILVER_PIECE
special SFUN_RANDOM_ZONE_MOVE time 60 bits SFB_RANTIME
end

			tw_zombie

names {"twisted zombie","zombie"}
title "a twisted zombie"
descr "A twisted zombie stands here staring aggressively at you."
extra {}
"An undead with sharp nails and claws, it eyes speaks of a certain
anger and hatred towards life."
M_ZOMBIE_TWISTED
money 5 COPPER_PIECE
special SFUN_AGGRESSIVE time WAIT_SEC*3 bits SFB_RANTIME
end

			dw_zombie

names {"dwarf zombie","zombie"}
title "a dwarf zombie"
descr "A dwarf zombie is here."
extra {}
"This is what remained of a stocky and noble warrior.  Now it is putrid,
pale-skinned and looks totally dreadful."
M_ZOMBIE_DWARF
money 1 SILVER_PIECE, 2 COPPER_PIECE
alignment -1000
end

			zombie

names {"zombie"}
title "a zombie"
descr "A zombie moves around here, with its claws spread wide."
extra {}
"What this putrid thing used to be, no one can tell."
M_ZOMBIE_HOLLOW
money 6 COPPER_PIECE
special SFUN_RANDOM_ZONE_MOVE time 60 bits SFB_RANTIME
end

			sw_ghoul

names {"swamp ghoul","ghoul"}
title "a swamp ghoul"
descr "A ghoul is here looking for corpses."
M_GHOUL_SWAMP(5, SEX_MALE)
money 1 SILVER_PIECE, 5 IRON_PIECE
special SFUN_AGGRESSIVE time WAIT_SEC*3 bits SFB_RANTIME
special SFUN_RANDOM_ZONE_MOVE time 60 bits SFB_RANTIME
end

                        zombie_capt

names {"stinking zombie","zombie"}
title "a stinking zombie"
descr "A stinking zombie moves around here, staring straight ahead."
extra {}
"This used to be a splendid knight, now turned into a dreadful undead."
M_ZOMBIE_STINKING
MSET_WEAPON(10,10,15,10,10,10)
money 1 SILVER_PIECE, 5 COPPER_PIECE
special SFUN_RANDOM_ZONE_MOVE time 60 bits SFB_RANTIME
end

                        huge_zombie

names {"huge zombie", "zombie"}
title "a huge zombie"
descr "A huge zombie moves around here, staring straight ahead"
extra {}
"This used to be a great Argan knight, now a dreadful undead."
M_ZOMBIE_AVG
money 1 SILVER_PIECE, 5 COPPER_PIECE
special SFUN_AGGRESSIVE time WAIT_SEC*3 bits SFB_RANTIME
end

                         huge_zombie2

names {"huge zombie", "zombie"}
title "a huge zombie"
descr "A huge zombie moves around here, staring straight ahead."
extra {}
"This used to be a great Argan knight, now a dreadful undead."
M_ZOMBIE_AVG
MSET_WEAPON(10,15,10,10,10,10)
money 1 SILVER_PIECE, 5 COPPER_PIECE
special SFUN_AGGRESSIVE time WAIT_SEC*3 bits SFB_RANTIME
end

/*------------------- CRYPT MOBS ---------------*/

                      	mummie

names {"mummie"}
title "a mummie"
descr "A mummie is here staggering towards you."
M_MUMMIE_AVG(SEX_NEUTRAL)
money 2 SILVER_PIECE, 5 COPPER_PIECE
special SFUN_AGGRESSIVE time WAIT_SEC*3 bits SFB_RANTIME
end


                  spirit

names {"spirit"}
title "a spirit"
descr
"A spirit lingers in the air."
extra {}
"It is nearly transparent, and appears very large."
M_SPIRIT_AVG(SEX_NEUTRAL)
money 6 SILVER_PIECE
flags {UNIT_FL_NO_TELEPORT}
special SFUN_AGGRESSIVE time WAIT_SEC*3 bits SFB_RANTIME
dilbegin prg1();
var
    item  : unitptr;
    sarco : unitptr;
code
{
    sarco := findunit(self, "sarcophagus", FIND_UNIT_SURRO, null);
    if (sarco != null)
    {
      if (sarco.inside == null)
      {
        exec("close sarcophagus", self);
        exec("lock sarcophagus", self);
        item := load("pendant@deadl1");
        item.height := sarco.height;
        link(item, sarco);
        item := load("ch_ring@deadl1");
        item.height := sarco.height;
        link(item, sarco);
      }
    }


    :start:
    heartbeat := PULSE_SEC * 10000;
    wait(SFB_TICK, FALSE);
    goto start;
}
dilend
end

/*--------------- NORTH ISLAND MOBS ------------------*/

                        mad_harriet

names {"mad harriet","harriet"}
title "Mad Harriet"
descr
"Mad Harriet stumbles around here, on clumsy feet"
extra {}
"She is an extremely ugly, old witch.  Her clothes are old
and outworn, except for her hat which seems quite new compared
to the rest of her outfit"

M_HUMAN_MAGE_GAS(17, SEX_FEMALE, "cast stinking cloud")
money 1 GOLD_PIECE, 2 SILVER_PIECE
flags {UNIT_FL_NO_TELEPORT}
romflags {CHAR_DETECT_INVISIBLE}
special SFUN_RANDOM_ZONE_MOVE time 180 bits SFB_RANTIME
special SFUN_PAIN_INIT time 50 bits SFB_RANTIME
" 5 *She's looking for her prince@
 10 S10@
 20 Cemote puts her filthy finger in her nose and finds something digestible.@
 30 R50,80@
 40 d10cast stinking cloud %s@
 50 R80,80@
 60 d10cast madness %s@
 80 d10cast toxicate %s@
 90 J10@"
end
		old_fool

names {"old fool","fool"}
title "the old fool"
descr
"The old fool is here telling nasty stories."
extra {}
"He is OLD."

M_HUMAN_MAGE_GAS(22, SEX_MALE, "cast stinking cloud")
money 1 GOLD_PIECE, 5 SILVER_PIECE
flags {UNIT_FL_NO_TELEPORT}
romflags {CHAR_DETECT_INVISIBLE}

/* Removed by Papi

   1) Guildless teachers are Ok, ONLY on the following conditions:

       a) A **very** tough quest is needed solved. Not just your
          ordinary fetch me this, fetch me that. A tough one, with
          puzzles & everything.

       b) After the quest is solved, the teacher can only be allowed
          to teach in a **very** limited category. I.e. he knows how
          to teach ONE thing when talking about spells & weapons &
          special skills. I.e. teach the acid ball, or teach the
          katana, or perhaps teach flee, consider & diagnostics
          (the last three are simple)

/*

/* NOTE BY PAPI: This is too many spells at once. Rouge teachers should be
   SPECIALIZED in similar stuff and not more than two to three skills.

This is not a regualar rouge teacher it is just a hard to find conjurer
teacher and requires that a player be conjuerer and level 70 to pra here.
*/

special SFUN_MEMBERS_ONLY GUILD_UDG_MAGE+
"#$1n says, 'You are not a Conjuerer, $3n. ONly conjurers can match my brains!'"

special SFUN_TEACH_INIT
"&lspells;0;
$1n tells you, 'I know this magic, but I do not teach it to you.';
$1n tells you, 'Ok I do not know how to teach this kind of magic. Or do I ?';
$1n tells you, 'You have not got %s for me, be gone moron.';
$1n tells you, 'You have not got %d skill points. How can you practice ?';
$1n tells you, 'I can not teach you any more, find a better teacher!';
$1n tells you, 'You must be unaffected by magic, otherwise I can't teach you';
$1n tells you, 'Remove all equipment, please.';

 70;  90; meteor shower                 ;   9;  9000;   10; 20;     0;
 "
end


          mad_cat

names {"mad cat","cat"}
title "a mad cat"
descr
"A huge, wierd cat patters here."
M_CAT(SEX_MALE)
money 2 COPPER_PIECE
end

                stink_toad

names {"stinking toad","toad"}
title "a stinking toad"
descr
"A large, fat toad is here, stinking awfully."

M_TOAD(SEX_FEMALE)
money 1 COPPER_PIECE
special SFUN_COMBAT_MAGIC "cast fart"
special SFUN_RANDOM_ZONE_MOVE time 60 bits SFB_RANTIME
special SFUN_PAIN_INIT time 120 bits SFB_RANTIME
" 5 *The toad ain't no prince function@
 10 Cemote farts loudly@
 20 S20@
 25 d10cast fart %s@"
end


/*--------------- SOUTH ISLAND MOBS --------------------*/

		breeder

names {"loska breeder","breeder","man"}
title "a loska breeder"
descr
"A stout man stands here guarding his loska."
extra {}
"He his a very big man, with humongous hands. His eyes are totally
green, like the colour of grass. His face appears somewhat compressed.
The very sight of him makes you want to laugh or puke."
M_HUMAN_WARRIOR_SWORD(10, SEX_MALE)
money 7 SILVER_PIECE
alignment 700
height 210
special SFUN_GUARD_WAY "1breed_dom@1-24@$1n stops you.@$1n prevents $3n from going east."
end

                loska_grow

names {"growing loska","loska"}
title "a growing loska"
descr
"A loska is growing here."
extra {}
"It is a tiny plant with round leaves and green berries."

flags {UNIT_FL_NO_TELEPORT}
M_LOSKA
level 9
money 2 SILVER_PIECE, 2 COPPER_PIECE
alignment 700
height 90
special SFUN_COMBAT_MAGIC "cast flash"
end

                loska

names {"loska"}
title "a loska"
descr "A loska is here."
extra {}
"It is a rather large plant. It has round darkgreen leaves,
some red berries hangs here and there in the branches."

M_LOSKA
level 10
money 2 SILVER_PIECE, 2 COPPER_PIECE
alignment 600
height 120

special SFUN_COMBAT_MAGIC "cast colourbeam"
special SFUN_RANDOM_ZONE_MOVE time 60 bits SFB_RANTIME
end

                  etho

names {"ethophadasus"}
title "Ethophadasus"
descr "Ethophadasus, the Sage of the Heavens stands here."
extra {}
"An elderly man sent down by the Gods to aid troubled adventurers."
flags {UNIT_FL_NO_TELEPORT}
race RACE_HUMAN
level 13
sex SEX_MALE
height 184
weight 203
alignment 1000
NATURAL_DEF(WPN_FIST, ARM_PLATE)
MSET_ABILITY(17,15,10,18,16,8,16,0)
MSET_WEAPON(10,15,7,7,5,7)
MSET_SPELL(1,15,1,1,1,5,5,5,5,5,5)
ATTACK_DEFENSE(0,+1000)
special SFUN_COMBAT_MAGIC "cast firestorm"

dilbegin prg2();
var pc : unitptr;
code
{
   heartbeat := PULSE_SEC * 5;
   on_activation(self.position <= POSITION_SLEEPING, skip);

   :start:
   wait(SFB_CMD, (command(CMD_BOW)) and (activator.type == UNIT_ST_PC));
   self.mana := 100;
   pc := activator;
   secure(pc, start);
   pause;
   exec("smile "+pc.name, self);
   pause;
   exec("point "+pc.name, self);
   exec("remove all", pc);
   pause;
   exec("cast dispel magic "+pc.name, self);
   pause;
   exec("say May the blessings of the Gods go with thee!", self);
   pause;
   self.mana := 100;
   self.hp := self.max_hp;
   self.endurance := 500;
   position_update(self);
   unsecure(pc);
   goto start;
}
dilend
end

/*--------------- SWAMP OBJECTS ---------------*/

%objects

                club2_50

names {"club"}
title "a large club"
descr "A large club lies here"
manipulate {MANIPULATE_TAKE, MANIPULATE_WIELD}
WEAPON_DEF(WPN_BATTLE_CLUB, 0, 0)
weight 20
cost 3 IRON_PIECE
end

                sword2_60

names {"cursed beltam","beltam", "sword"}
title "cursed Beltam"
descr "Beltam, the cursed, large two handed sword lies here."
extra {}
"The sword is not in its best condition. The name Beltam is
graved on the blade."
manipulate {MANIPULATE_TAKE, MANIPULATE_WIELD}

/* Steel + good craftman ship + mistreatment = (120 + 10 + -10%) */
WEAPON_DEF(WPN_GREAT_SWORD, 0, -5)
weight 18
cost 250 COPPER_PIECE
rent 25 IRON_PIECE
CHA_TRANSFER(-5)
STR_TRANSFER(1)
CURSED_OBJECT
end

                fool_sword

names {"gimelnjord","pole axe","axe"}
title "Gimelnjord"
descr "Gimelnjord the large pole axe."
extra {}
"The pole axe seems to be of ancient times."
manipulate {MANIPULATE_TAKE, MANIPULATE_WIELD}
WEAPON_DEF(WPN_HALBERD, 1, 1)
weight 30
cost 910 COPPER_PIECE
rent 91 IRON_PIECE
BRA_TRANSFER(-3)
CHAR_FLAG_TRANSFER(CHAR_OUTLAW)
special SFUN_RESTRICT_OBJ "Str = 75"
end


                dag0_20

names {"wierd-looking dagger","dagger"}
title "a wierd-looking dagger"
descr "A wierd-looking dagger lies here."
extra {}
"It is a small dagger, with a golden blade."
manipulate {MANIPULATE_TAKE, MANIPULATE_WIELD}
/* Not pure iron + (witch)craftman ship = (90 + 20%) */
WEAPON_DEF(WPN_DAGGER, 0, 0)
weight 1
cost 700 COPPER_PIECE
rent 70 IRON_PIECE
DEX_TRANSFER(-3)
STR_TRANSFER(-3)
CHAR_FLAG_TRANSFER(CHAR_PROTECTED)
special SFUN_RESTRICT_OBJ "Bra = 60"
end

                black_hat

names {"black hat","pointy hat","hat"}
title "a black, pointy hat"
descr "A black, pointy hat lies here."
extra {}
"The hat is made of velvet and feels very soft."
manipulate {MANIPULATE_TAKE, MANIPULATE_WEAR_HEAD}
/* Gives protection as a leather cap */
ARMOUR_LEATHER(0, 0)
weight 1
cost 700 COPPER_PIECE
rent 70 IRON_PIECE
DEX_TRANSFER(-2)
special SFUN_RESTRICT_OBJ "Bra = 60"
end

/*----------------- OBJECTS IN THE CRYPT ----------*/

		sarcophagus

names {"sarcophagus"}
title "the sarcophagus"
descr "A sarcophagus is mounted to the floor in the centre of the room."
extra {} "You notice a triangular hole on one side of the sarcophagus."
manipulate {MANIPULATE_ENTER}
open {EX_OPEN_CLOSE, EX_CLOSED, EX_LOCKED, EX_PICKPROOF}
flags {UNIT_FL_INDOORS, UNIT_FL_NO_WEATHER}
key triangular_key
CONTAINER_DEF(800)
end

		triangular_key

names {"triangular staff", "staff"}
title "a staff"
descr "A triangular staff has been left here."
type ITEM_KEY
manipulate {MANIPULATE_TAKE, MANIPULATE_HOLD}
flags {UNIT_FL_NOSAVE}
weight 1
cost 1 IRON_PIECE
rent 1 IRON_PIECE
extra {} "It is a triangular staff made of crystal."
end

                pendant

names {"small pendant","pendant"}
title "a small pendant, of sagacity"
descr "A small pendant lies here"
type ITEM_OTHER
manipulate {MANIPULATE_TAKE, MANIPULATE_WEAR_NECK}
weight 1
cost 250 COPPER_PIECE
rent 25 IRON_PIECE
extra {}
"It is a representation of Mimers head."
special SFUN_RESTRICT_OBJ "Bra = 25"
end

		ch_ring

names {"golden ring","ring"}
title "a golden ring"
descr "A golden ring lies here"
type ITEM_OTHER
manipulate {MANIPULATE_TAKE, MANIPULATE_WEAR_FINGER, MANIPULATE_HOLD}
weight 1
cost 250 COPPER_PIECE
rent 25 IRON_PIECE
extra {}
"It is a ring made of pure gold. A raven is graved on it"
special SFUN_RESTRICT_OBJ "Cha = 25"
end


                      tome

names {"a large tome", "tome", "book"}
title "a large tome"
descr "A large tome rests on a book stand."
extra {}
"Here is a list of zones and the persons to contact.  It does not follow
that the person listed must be creators of that zone.  Many of the
zones are not listed here.  Some I may have missed out, others were
created by designers who left us a long time ago.  For such zones
please send your mudmails to me.

                                 - Thesar

Guilds

Assassins                - Vanyel
Mystics                  - Icculus
Necromancer              - Saor
Paladins                 - Bakka / Whistler
Sorcerers                - Icculus

Zones

Arctic                   - Thesar
Asgaard                  - Icculus
Atlantis                 - Anubis
Citadel                  - Saor
Death Keep               - Thesar
Death Swamps             - Thesar
Farginok                 - Bakka / Whistler
Hades (1st-5th Plane)    - Icculus
Hades (7th-8th Plane)    - Kilbia
Hermgaard (Urland)       - Elminster
Lostlands                - Cabal
Ratswarf                 - Telegrim
Tassle Grove (Halfling)  - Whistler / Bakka
Tiamat's temple          - Icculus
Udgaard Sewers           - Drevar
Underdark                - Elminster
Volcano (Danalek City)   - Elminster

"
type ITEM_BOOK
weight 200
end

/*------------------- ZONE RESET ------------------------*/
%reset

/*------------------- OPERATE DOORS ------------------------*/


/*------------------- SWAMP RESET ------------------------*/

load etho into midgaard/park_circle max 1

load zombie into de_swamp1

load tw_zombie into de_swamp4

load tw_zombie into de_swamp2

load dw_zombie into base_mount

load sw_ghoul into de_swamp12

load sw_ghoul into de_swamp6

load ho_zombie into de_swamp9

load ho_zombie into de_swamp18

load huge_zombie2 into de_swamp8
{
	equip sword2_60 position WEAR_WIELD
}

load huge_zombie into de_swamp18

load zombie_capt into de_swamp16
{
	equip club2_50 position WEAR_WIELD
}

/*----------------- CRYPT RESET --------------*/

load mummie into tomb

load spirit into crypt
{
	equip triangular_key position WEAR_HOLD
}

load sarcophagus into crypt
{
	load pendant zonemax 1
	load ch_ring zonemax 1
}

/*----------------- SOUTH ISLAND RESET --------------*/

load breeder into so_isl1

load loska_grow into garden

load loska_grow into garden

load loska_grow into garden

load loska into meadow

load loska into breed_dom

/*----------------- NORTH ISLAND RESET --------------*/

load mad_harriet into cottage3
{
	equip black_hat position WEAR_HEAD
	equip dag0_20 position WEAR_WIELD
}

load stink_toad into no_isl3

load stink_toad into no_isl1

load mad_cat into cottage2

load old_fool into cottage3
{
	load fool_sword
}


%end


