/*
filename    hades1
password    2wsl5
changedby   Zandar
EmailAdd     Palio <palio@blinksoft.com>
request     compile
version     4
END HEADER*/

#include "composed.h"
#include "values.h"
#include "wmacros.h"

%zone hades1
title "Hell Planes 1 - 8"
lifespan 15
reset RESET_ANYHOW
creators {"icculus", "ford"}

notes
"The Hades Zone rewrites! Most of the original file has been discarded
since it didn't reflect a gothic enough Hell for Valhalla. This file
contains the Midgaard Connections made by Ford, and planes 1,3 and 5
made by Icculus (Andrew Cowan). All remaining planes will be connected
accordingly and will be found in the files: hades2.zon, hades3.zon,
hades4.zon and possibly hades6.zon. Any modifications, suggestions or
comments should be sent to me, my current email: agcowan@turing.uncg.edu.
These addresses won't last forever though but you can assuredly find me on
Valhalla.


oct 11/95 The hades levels where split into seperate zones for ease of
upkeep.
hades now include hades 1,2,3,4,5,7,8
"

/* ************************* Needed Defs for Zone ********************** */
#define		HOLD		wait(SFB_TICK, TRUE);
#define		HELD		"Player Held"
#define		VACANT		"vacant"
/* ********************************************************************* */

%rooms

/* *********************** Plane 1 of Hell ************************ */

				ent_hell

names {"entrance hell"}
title "The Entrance to the First Plane"
descr
"   After regaining your sense of direction, you realize your curiosity
may have cost you dearly this time. You stand looking out over the caverns
and fissures making up the first plane of hell. Most of this land is bare
but the heat is unbearable and occassionally small fires burst from the
ground all about you. You hear a very steady stream of moans and shrieks
from every direction. Directly in front of you a large gate of fire blocks
you from proceeding any further."
extra {"fiery gate", "gate"}
"   The material which forged this gate must be very fire retardant. A fire
whose intensity darkens your skin covers the entire gate. Upon closer
inspection you realize the flames engrossing the gate of hell are not hot.
You think you can actually touch it."
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
north to plane1_rm1 open {EX_OPEN_CLOSE, EX_CLOSED} descr
 "The gate to hell is there." keyword {"gate to hell", "gate", "fiery gate"};
/* Added by Papi, I didn't understand why it was missing... ? */
up to cave@volcano;
end

				plane1_rm1
title "The First Plane of hell"
descr
"   The fiery gates you passed through to get here are no longer visible
to your eyes. The feeling of being caged sets in and you become aware that
your survival depends on your powers of observation. You must battle the
psychic confusion cast about your very existence to get out of here, for it
is unlikely the exits, if any, will be easily found."
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
north to plane1_rm2;
east to plane1_rm3;
west to plane1_rm4;
end

				plane1_rm2

title "the First Plane of hell"
descr
"   There seems to be no distinguishing features here at all. Every
direction yields a similar view, fiery caverns and brilliant red soil
covering the grounds beneath. Shadows dance and play formed from the
glow of several fires in the vicinty."
extra {"shadow", "shadows"}
"   They look like they could belong to any human-like form, but the
way they move is unlike that of anything you have known. Nothing in
the world you belong to can morph its body like this!"
extra {"fiery caverns", "caverns"}
"You cannot seem to to see very far inside, the scorching flames
block any visuals inside the caverns."
extra {"brilliant red soil", "red soil", "soil"}
"The soil seems unnaturally bright, as if the fires of hell burned inside
every grain!"
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
east to plane1_rm5;
west to plane1_rm6;
south to plane1_rm1;
end

				plane1_rm3

title "the First Plane of hell"
descr
"   There seems to be no distinguishing features here at all. Every
direction yields a similar view, fiery caverns and brilliant red soil
covering the grounds beneath. Shadows dance and play formed from the
glow of several fires in the vicinty."
extra {"shadow", "shadows"}
"   They look like they could belong to any human-like form, but the
way they move is unlike that of anything you have known. Nothing in
the world you belong to can morph its body like this!"
extra {"fiery caverns", "caverns"}
"You cannot seem to to see very far inside, the scorching flames
block any visuals inside the caverns."
extra {"brilliant red soil", "red soil", "soil"}
"The soil seems unnaturally bright, as if the fires of hell burned inside
every grain!"
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
north to plane1_rm5;
west to plane1_rm1;
end

                                plane1_rm4

title "the First Plane of hell"
descr
"   There seems to be no distinguishing features here at all. Every
direction yields a similar view, fiery caverns and brilliant red soil
covering the grounds beneath. Shadows dance and play formed from the
glow of several fires in the vicinty."
extra {"shadow", "shadows"}
"   They look like they could belong to any human-like form, but the
way they move is unlike that of anything you have known. Nothing in
the world you belong to can morph its body like this!"
extra {"fiery caverns", "caverns"}
"You cannot seem to to see very far inside, the scorching flames
block any visuals inside the caverns."
extra {"brilliant red soil", "red soil", "soil"}
"The soil seems unnaturally bright, as if the fires of hell burned inside
every grain!"
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
north to plane1_rm6;
east to plane1_rm1;
end

                                plane1_rm5

title "the First Plane of hell"
descr
"   There seems to be no distinguishing features here at all. Every
direction yields a similar view, fiery caverns and brilliant red soil
covering the grounds beneath. Shadows dance and play formed from the
glow of several fires in the vicinty."
extra {"shadow", "shadows"}
"   They look like they could belong to any human-like form, but the
way they move is unlike that of anything you have known. Nothing in
the world you belong to can morph its body like this!"
extra {"fiery caverns", "caverns"}
"You cannot seem to to see very far inside, the scorching flames
block any visuals inside the caverns."
extra {"brilliant red soil", "red soil", "soil"}
"The soil seems unnaturally bright, as if the fires of hell burned inside
every grain!"
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
north to plane1_rm11;
west to plane1_rm2;
south to plane1_rm3;
end

                                plane1_rm6

title "the First Plane of hell"
descr
"   There seems to be no distinguishing features here at all. Every
direction yields a similar view, fiery caverns and brilliant red soil
covering the grounds beneath. Shadows dance and play formed from the
glow of several fires in the vicinty."
extra {"shadow", "shadows"}
"   They look like they could belong to any human-like form, but the
way they move is unlike that of anything you have known. Nothing in
the world you belong to can morph its body like this!"
extra {"fiery caverns", "caverns"}
"You cannot seem to to see very far inside, the scorching flames
block any visuals inside the caverns."
extra {"brilliant red soil", "red soil", "soil"}
"The soil seems unnaturally bright, as if the fires of hell burned inside
every grain!"
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
north to plane1_rm7;
east to plane1_rm2;
south to plane1_rm4;
end

                                plane1_rm7

title "the First Plane of hell"
descr
"   There seems to be no distinguishing features here at all. Every
direction yields a similar view, fiery caverns and brilliant red soil
covering the grounds beneath. Shadows dance and play formed from the
glow of several fires in the vicinty."
extra {"shadow", "shadows"}
"   They look like they could belong to any human-like form, but the
way they move is unlike that of anything you have known. Nothing in
the world you belong to can morph its body like this!"
extra {"fiery caverns", "caverns"}
"You cannot seem to to see very far inside, the scorching flames
block any visuals inside the caverns."
extra {"brilliant red soil", "red soil", "soil"}
"The soil seems unnaturally bright, as if the fires of hell burned inside
every grain!"
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
west to plane1_rm8;
south to plane1_rm6;
end

                                plane1_rm8

title "the First Plane of hell"
descr
"   There seems to be no distinguishing features here at all. Every
direction yields a similar view, fiery caverns and brilliant red soil
covering the grounds beneath. Shadows dance and play formed from the
glow of several fires in the vicinty."
extra {"shadow", "shadows"}
"   They look like they could belong to any human-like form, but the
way they move is unlike that of anything you have known. Nothing in
the world you belong to can morph its body like this!"
extra {"fiery caverns", "caverns"}
"You cannot seem to to see very far inside, the scorching flames
block any visuals inside the caverns."
extra {"brilliant red soil", "red soil", "soil"}
"The soil seems unnaturally bright, as if the fires of hell burned inside
every grain!"
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
north to plane1_rm9;
east to plane1_rm7;
west to rocky_tunnel1;
end

                                plane1_rm9

title "the First Plane of hell"
descr
"   There seems to be no distinguishing features here at all. Every
direction yields a similar view, fiery caverns and brilliant red soil
covering the grounds beneath. Shadows dance and play formed from the
glow of several fires in the vicinty."
extra {"shadow", "shadows"}
"   They look like they could belong to any human-like form, but the
way they move is unlike that of anything you have known. Nothing in
the world you belong to can morph its body like this!"
extra {"fiery caverns", "caverns"}
"You cannot seem to to see very far inside, the scorching flames
block any visuals inside the caverns."
extra {"brilliant red soil", "red soil", "soil"}
"The soil seems unnaturally bright, as if the fires of hell burned inside
every grain!"
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
north to plane1_rm9;
west to plane1_rm10;
south to plane1_rm8;
end

                                plane1_rm10

title "the First Plane of hell"
descr
"   There seems to be no distinguishing features here at all. Every
direction yields a similar view, fiery caverns and brilliant red soil
covering the grounds beneath. Shadows dance and play formed from the
glow of several fires in the vicinty."
extra {"shadow", "shadows"}
"   They look like they could belong to any human-like form, but the
way they move is unlike that of anything you have known. Nothing in
the world you belong to can morph its body like this!"
extra {"fiery caverns", "caverns"}
"You cannot seem to to see very far inside, the scorching flames
block any visuals inside the caverns."
extra {"brilliant red soil", "red soil", "soil"}
"The soil seems unnaturally bright, as if the fires of hell burned inside
every grain!"
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
east to plane1_rm9;
west to plane1_rm10;
end

                                 plane1_rm11

title "the First Plane of hell"
descr
"   There seems to be no distinguishing features here at all. Every
direction yields a similar view, fiery caverns and brilliant red soil
covering the grounds beneath. Shadows dance and play formed from the
glow of several fires in the vicinty."
extra {"shadow", "shadows"}
"   They look like they could belong to any human-like form, but the
way they move is unlike that of anything you have known. Nothing in
the world you belong to can morph its body like this!"
extra {"fiery caverns", "caverns"}
"You cannot seem to to see very far inside, the scorching flames
block any visuals inside the caverns."
extra {"brilliant red soil", "red soil", "soil"}
"The soil seems unnaturally bright, as if the fires of hell burned inside
every grain!"
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
east to marble_court1;
south to plane1_rm5;
west to plane1_rm11;
end

				marble_court1

names {"marble court"}
title "A Marble Court"
descr
"   This a nice change from the monotony you have been traversing
thus far in your journey. Your spirit picks up as you realize the
end of the first plane is nearby.
    A steel railing runs the perimeter of the room, connecting to
similar railings in adjacent rooms. You must be careful around them,
a glance over their boundaries shows you what you would look like as
a victim of fire abuse."
extra {"railing", "rail"}
"   A simple railing to the eye, its supernatural strength is unknown
to most. At times, it is all that will keep you safe from the winds
of hell."
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
north to marble_court3;
east to marble_court2;
west to plane1_rm11;
end

                                marble_court2

title "A Marble Court"
descr
"   This a nice change from the monotony you have been traversing
thus far in your journey. Your spirit picks up as you realize the
end of the first plane is nearby.
    A steel railing runs the perimeter of the room, connecting to
similar railings in adjacent rooms. You must be careful around them,
a glance over their boundaries shows you what you would look like as
a victim of fire abuse."
extra {"railing", "rail"}
"   A simple railing to the eye, its supernatural strength is unknown
to most. At times, it is all that will keep you safe from the winds
of hell."
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
north to marble_court4;
west to marble_court1;
end

                                marble_court3

title "A Marble Court"
descr
"   This a nice change from the monotony you have been traversing
thus far in your journey. Your spirit picks up as you realize the
end of the first plane is nearby.
    A steel railing runs the perimeter of the room, connecting to
similar railings in adjacent rooms. You must be careful around them,
a glance over their boundaries shows you what you would look like as
a victim of fire abuse. You notice a boulder in the wall which looks
to be out of place, perhaps you can move it."
extra {"railing", "rail"}
"   A simple railing to the eye, its supernatural strength is unknown
to most. At times, it is all that will keep you safe from the winds
of hell."
extra {"boulder"}
"It looks like if you searched, you might find something more than just
a boulder here!"
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
north to rope_bridge open {EX_OPEN_CLOSE, EX_CLOSED, EX_HIDDEN}
      keyword {"door", "boulder"};
SECRET_DOOR_DIFFICULTY(NORTH, 60)
east to marble_court4;
south to marble_court1;
end

                                marble_court4
title "A Marble Court"
descr
"   This a nice change from the monotony you have been traversing
thus far in your journey. Your spirit picks up as you realize the
end of the first plane is nearby.
    A steel railing runs the perimeter of the room, connecting to
similar railings in adjacent rooms. You must be careful around them,
a glance over their boundaries shows you what you would look like as
a victim of fire abuse."
extra {"railing", "rail"}
"   A simple railing to the eye, its supernatural strength is unknown
to most. At times, it is all that will keep you safe from the winds
of hell."
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
west to marble_court3;
south to marble_court2;
end

				rope_bridge
names {"rope bridge"}
title "A Rope Bridge"
descr
"   If you had a choice this bridge would surely not be traversed, but
you have a deep rooted feeling that you must cross it. Testing the bridge
reveals a rather unnerving creaking sound from the rope connections. There
is a lit cavern north of here and to the south is a door formed out of
a large boulder. Somehow you feel a bit relieved, your natural intuition
tells you this room lacks the magical barrier you sensed in every corner
of hell. Though you can not reach this realm through teleportation you
feel you can exit back to the mortal realm from here.
    There is a small hole in the floor of this area and if you enter
it you might possibly find your way out."
extra {"hole in the floor", "hole", "floor"}
"There is a small hole here in the floor. You can enter this
hole and teleport yourself back to safety."
extra {"boulder", "door"}
"There is a door formed from a boulder here."
flags {UNIT_FL_NO_MOB, UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
north to lit_cavern1;
south to marble_court3 open {EX_OPEN_CLOSE, EX_CLOSED} descr
      "A small boulder door." keyword {"boulder", "door"};

/* Dil to allow safe passage back to the mortal realm. */

dilbegin prg2();
var
        pc      :       unitptr;
code
{
    :init:
    heartbeat := PULSE_SEC*5;

    :start:
    wait(SFB_CMD, ((command(CMD_ENTER)) and (("floor" in argument) or
        ("hole" in argument))));

    pc := activator;
    secure(pc, lost_pc);
    block;
    pause;
    act("You find yourself fading into another existance as you pass through.",
        A_ALWAYS, pc, null, null, TO_CHAR);
    act(pc.name+" enters the hole in the floor and vanishes.",
        A_SOMEONE, pc, null, null, TO_REST);
    link(pc, findroom("udgaard/temple"));
    act("You find yourself emerging in the temple of Udgaard.",
        A_ALWAYS, pc, null, null, TO_CHAR);
    goto start;

    :lost_pc:
    pause;
    goto start;
}
dilend

end

				lit_cavern1

names {"lit cavern"}
title "A Lit Cavern"
descr
"   Your nerves have a brief moment to rejoice after successfuly
traversing the shabby bridge. You now stand at the other end in
a somewhat lighted cavern. The only source you can see are the many
torches nailed into the cavern wall, and their fires look to be
dimming with every passing breath."
flags {UNIT_FL_NO_WEATHER}
extra {"torches", "torch"}
"They are fastened to the cavern wall, unremovable but the do provide
a meager sort of light source for the time being."
movement SECT_INSIDE
north to large_cavern;
south to rope_bridge;
east to lit_cavern2;
west to lit_cavern3;
ALWAYS_LIGHT
end

				large_cavern
names {"large cavern", "cavern"}
title "A Large Cavern"
descr
"   You exit the lit cavern into a somewhat dismal one. You notice the
rock walls form a circular perimeter about the room and though you cannot
tell for sure, it seems almost perfectly symmetric. Besides the normal
rocks and dust scattered about the ground there is a large mat placed
in the center. The low lighting makes it difficult to see clearly though."
extra {"rock walls", "walls", "wall"}
"They are quite smooth, almost like they were specially created to be
such. The walls form a symmetric sphere about you."
extra {"rocks", "dust", "rock"}
"There are many rocks scattered about the ground, most likely the
remains of any jagged edges since removed. The dust is almost too
much for you, it is apparent the inhabitants do not suffer from
allergies."
extra {"large mat", "mat"}
"It is hard to discern the material this mat is made of, in fact
you can barely see it from here but it seems the room was formed
around it."
movement SECT_INSIDE
flags {UNIT_FL_NO_WEATHER}
south to lit_cavern1;
IN_ALWAYS_DARK
end

                                lit_cavern2

title "A Lit Cavern"
descr
"   You are in a similar cavern, in fact it would be hard to tell
that you have left the previous room. As you explore your surroundings
you notice these torches are getting quite dim, their light will not
be noticable very shortly."
flags {UNIT_FL_NO_WEATHER}
extra {"torches", "torch"}
"They are fastened to the cavern wall, unremovable but the do provide
a meager sort of light source for the time being."
movement SECT_INSIDE
east to lit_cavern4;
west to lit_cavern1;
ALWAYS_LIGHT
end

                                lit_cavern3

title "A Lit Cavern"
descr
"   You are in a similar cavern, in fact it would be hard to tell
that you have left the previous room. As you explore your surroundings
you notice these torches are getting quite dim, their light will not
be noticable very shortly."
flags {UNIT_FL_NO_WEATHER}
extra {"torches", "torch"}
"They are fastened to the cavern wall, unremovable but the do provide
a meager sort of light source for the time being."
movement SECT_INSIDE
east to lit_cavern1;
west to lit_cavern5;
ALWAYS_LIGHT
end

                                lit_cavern4

title "A Lit Cavern"
descr
"   The fires on the torches are just about out now, and your eyes must
strain to make out the slightest detail. Your sense of sound does not
fail you however and you can definately hear a groaning noise, like
the sounds of a floor giving way beneath your feet!"
flags {UNIT_FL_NO_WEATHER}
movement SECT_INSIDE
west to lit_cavern2;
special SFUN_FORCE_MOVE
"hades2/molt_entrance!The floor gives way!" time WAIT_SEC*10 bits SFB_RANTIME
end

                                lit_cavern5

title "A Lit Cavern"
descr
"   The fires on the torches are just about out now, and your eyes must
strain to make out the slightest detail. Your sense of sound does not
fail you however and you can definately hear a groaning noise, like
the sounds of a floor giving way beneath your feet!"
movement SECT_INSIDE
flags {UNIT_FL_NO_WEATHER}
east to lit_cavern3;
special SFUN_FORCE_MOVE
"hades2/molt_entrance!The floor gives way!" time WAIT_SEC*10 bits SFB_RANTIME
end

				rocky_tunnel1
names {"rocky tunnel", "tunnel"}
title "A Rocky Tunnel"
descr
"   This tunnel seems to veer off from the rest of the first plane.
The ground you walk and tunnel walls all appear much darker than
those of the entrance to this plane. You notice the darkness is
from the long collection of a smokey resin with an as of yet unknown
cause. The tunnel continues to the west."
flags {UNIT_FL_NO_WEATHER, UNIT_FL_NO_MOB}
extra {"tunnel walls", "walls", "ground"}
"There is a smokey substance plastered to every square inch of
this rock. What could have caused so much smoke?"
movement SECT_INSIDE
east to plane1_rm8;
west to rocky_tunnel2;

/* Dil to stop low level chars from entering a bad zone! */

dilbegin prg3();
code
{
   :start:
   wait(SFB_CMD, ((activator.type == UNIT_ST_PC) and
       (command(CMD_WEST))));

   if (activator.level < 30)
   {
      block;
      act("You feel thwarted, a giant hand shimmers to life " +
	  "before your eyes and blocks your exit to the west.",
          A_ALWAYS, activator, null, null, TO_CHAR);
      act(activator.name+" is stopped by a shimmering hand from exiting.",
	   A_SOMEONE, activator, null, null, TO_REST);
      act("A loud voice booms 'Levels 30 and above may exit west'.",
           A_ALWAYS, activator, null, null, TO_ALL);
      goto start;
    }
}
dilend

end

                                rocky_tunnel2
names {"rocky tunnel", "tunnel"}
title "A Rocky Tunnel"
descr
"   This tunnel seems to veer off from the rest of the first plane.
The ground you walk and tunnel walls all appear much darker than
those of the entrance to this plane. You notice the darkness is
from the long collection of a smokey resin with an as of yet unknown
cause. The tunnel continues to the west."
flags {UNIT_FL_NO_WEATHER, UNIT_FL_NO_MOB}
extra {"tunnel walls", "walls", "ground"}
"There is a smokey substance plastered to every square inch of
this rock. What could have caused so much smoke?"
movement SECT_INSIDE
east to rocky_tunnel1;
west to rocky_tunnel3;
end

                                rocky_tunnel3
names {"rocky tunnel", "tunnel"}
title "A Rocky Tunnel"
descr
"   This tunnel seems to veer off from the rest of the first plane.
The ground you walk and tunnel walls all appear much darker than
those of the entrance to this plane. You notice the darkness is
from the long collection of a smokey resin with an as of yet unknown
cause. There is a rocky doorway to the west, a foul odor looms in
that direction. Perhaps you will be brave and enter it?"
flags {UNIT_FL_NO_WEATHER, UNIT_FL_NO_MOB}
extra {"tunnel walls", "walls", "ground"}
"There is a smokey substance plastered to every square inch of
this rock. What could have caused so much smoke?"
extra {"rocky doorway", "doorway", "door"}
"It is malicious looking as far as doorways go, but you feel you
might be overcome by your unrestrained curiosity."
movement SECT_INSIDE
east to rocky_tunnel2;

/* Dil to simulate a door */

dilbegin prg4();
var
        pc      :       unitptr;
        room    :       unitptr;
code
{
   :start:
   wait(SFB_CMD, ((activator.type == UNIT_ST_PC) and (command(CMD_ENTER)) and
        (("rocky doorway" in argument) or ("doorway in argument") or
	("door" in argument))));

   block;
   pc := activator;
   secure(pc, lost_pc);

   room := findroom("moun_cav27@tiamat");

   act("You enter the door and peer out on the oother side.",
        A_ALWAYS, pc, null, null, TO_CHAR);
   act(pc.name+" enters the door vanishes.",
        A_SOMEONE, pc, null, null, TO_REST);
   link(pc, room);

   unsecure(pc);
   :lost_pc:
   exec("look", pc);

   act(pc.name+" suddenly emerges from the door.",
        A_SOMEONE, pc, null, null, TO_REST);
   goto start;
}
dilend

end



/* ******************** Mobiles of planes 1 ********************* */



%mobiles

/* ******************** Mobiles for Midgaard Con. ********************* */

				guard_devil

names {"guardian devil","guardian","devil"}
title "the Guardian Devil"
descr
"At the foot of the stairs floats a malovelent devil with his arms crossed and
an evil grin streching his face."
extra {}
"The Guardian Devil is perhaps the most evil creature you will ever lay eyes
on if you are fortunate.  He is deep red in color, has his lower half of his
body conceled by fire, and holds a flaming trident.  He looks at you like
you are the insignificant creature you are in his eyes."
romflags {CHAR_DETECT_INVISIBLE, CHAR_DETECT_LIFE}
level 60
alignment -1000
race RACE_GREATER_DEVIL
sex SEX_MALE
weight 1500
NATURAL_DEF(WPN_FIST, ARM_PLATE)
MSET_ABILITY (5,5,20,30,10,10,10,10)
MSET_WEAPON (0,0,0,35,0,20)
MSET_SPELL (25,0,0,0,0,0,20,0,0,0,0)
exp 200
money 1 GOLD_PIECE, 4 SILVER_PIECE

flags {UNIT_FL_NO_TELEPORT}


dilcopy combat_mag@function("fireball", "cure serious wounds", 25, 2);

/* Dil to block newbies from entering hell */

dilbegin prg16();

code
{
   heartbeat := PULSE_SEC*5;
   :start:
   wait(SFB_CMD, (activator.type == UNIT_ST_PC) and
       (command(CMD_DOWN)));

   if (activator.level < 30)
   {
      exec("say You may not go in there my young friend! " +
           "For it is my duty to keep such inferiority from " +
 	   "the planes!", self);
      block;
   }
   goto start;
}
dilend

end

/* ********************** Mobiles for Plane 1 ************************** */

				erinyes

names {"erinyes"}
title "Erinyes"
descr
"Erinyes the Collector of Souls is here."
extra {}
"This creature vaguely resembles a woman, yet her skin looks more like
an armor and on her shoulders sprouts large shaggy hawklike wings.
When she is not dragging evil mortals to their place in hell, she can
be found trying to tempt anyone into evil doing. Her job in hell is to
garner souls for her lord Asmodeus."
romflags {CHAR_DETECT_INVISIBLE, CHAR_DETECT_LIFE}
level 37
alignment -900
race RACE_OTHER_UNDEAD
flags {UNIT_FL_NO_TELEPORT}
sex SEX_FEMALE
MSET_ABILITY (30,10,10,10,30,0,10,0)
MSET_WEAPON (0,0,0,0,55,0)
MSET_SPELL (0,0,0,0,0,0,45,0,0,0,0)
NATURAL_DEF(WPN_CLAW, ARM_CHAIN)
weight 850
exp 125
money 4 SILVER_PIECE, 5 COPPER_PIECE

dilcopy combat_mag@function("fireball","", 25, 2);

special SFUN_PAIN_INIT time WAIT_SEC*15 bits SFB_RANTIME
"  100 Csnarl@
   110 AThe Erinyes mutters 'Yoouurr Soouull...Give me yoouurr soouul.'@
   120 AThe Erinyes spreads her wings and cries an inhuman shriek.@
   130 AThe Erinyes mutters 'You will be rewarded by the damned...'@
   140 AThe Erinyes casts a gaze in your direction and screams!@
"
end

				lemure_type1

names {"lemure"}
title "a small Lemure"
descr
"A disgusting Lemure slides across the floor towards you."
extra {}
"These are truly disgusting creatures whose flesh seems to fall
off in glops onto the ground. When mortal souls are brought into
hell, this is what they are turned into!"
level 35
alignment -900
race RACE_OTHER_UNDEAD
flags {UNIT_FL_NO_TELEPORT}

sex SEX_MALE
MSET_ABILITY (15,5,10,25,25,0,10,10)
MSET_WEAPON (0,0,0,0,55,0)
MSET_SPELL (0,0,0,0,0,0,45,0,0,0,0)
NATURAL_DEF(WPN_FIST, ARM_CHAIN)
weight 600
exp 110
money 4 SILVER_PIECE, 2 COPPER_PIECE
special SFUN_RANDOM_ZONE_MOVE time 40 bits SFB_RANTIME
special SFUN_PAIN_INIT time WAIT_SEC*20 bits SFB_RANTIME
"  100 AThe Lemure slides along the floor towards you.@
   110 AThe Lemure moans in agony and torment.@
   120 AA rather large chunk of flesh oozes from the Lemure.@
   130 AThe Lemure's right arm slowly tears from socket.@
   140 AThe Lemure's right arm falls to the ground in a slimy heap.@
"
end

                                lemure_type2

names {"lemure"}
title "a Lemure"
descr
"A disgusting Lemure slides across the floor towards you."
extra {}
"These are truly disgusting creatures whose flesh seems to fall
off in glops onto the ground. When mortal souls are brought into
hell, this is what they are turned into!"
level 35
alignment -900
race RACE_OTHER_UNDEAD
flags {UNIT_FL_NO_TELEPORT}

sex SEX_MALE
MSET_ABILITY (15,5,10,25,25,0,10,10)
MSET_WEAPON (0,0,0,0,55,0)
MSET_SPELL (0,0,0,0,0,0,45,0,0,0,0)
NATURAL_DEF(WPN_FIST, ARM_CHAIN)
weight 850
exp 105
money 4 SILVER_PIECE,21 IRON_PIECE

special SFUN_AGGRESSIVE time WAIT_SEC*4  bits SFB_RANTIME

dilcopy combat_mag@function("fireball","", 25, 2);

special SFUN_RANDOM_ZONE_MOVE time 40 bits SFB_RANTIME
special SFUN_PAIN_INIT time WAIT_SEC*20 bits SFB_RANTIME
"  100 AThe Lemure slides along the floor towards you.@
   110 AThe Lemure moans in agony and torment.@
   120 AThe Lemure's chest cavity pulses and writhes.@
   130 AA large chunk of flesh falls to the ground.@
"
end

                                giant_lemure

names {"giant lemure", "lemure", "giant"}
title "the Giant Lemure"
descr
"The Giant Lemures sits here on his mat."
extra {}
"If you thought the lemures roaming the caverns of hell were
disgusting, this one must redefine the term. He is a large mass
of disgusting gray flesh, actually he is more like many large heaps
being that chunks of flesh are constantly sliding off and oozing
to the mat below him. He sits quietly on his mat, eyes shut and
motionless. You wonder what purpose could this creature possibly
serve here in Hell?"
level 38
alignment -900
flags {UNIT_FL_NO_TELEPORT}

race RACE_OTHER_UNDEAD
sex SEX_MALE
height 300
weight 1100
MSET_ABILITY (20,15,10,25,20,0,5,5)
MSET_WEAPON (0,0,0,0,50,0)
MSET_SPELL (0,0,0,0,0,0,50,0,0,0,0)
NATURAL_DEF(WPN_FIST, ARM_PLATE)
exp 105
position POSITION_SITTING
default POSITION_SITTING
money 4 SILVER_PIECE,4 COPPER_PIECE

dilcopy  combat_mag@function("fireball","" , 25, 2);

special SFUN_PAIN_INIT time WAIT_SEC*20 bits SFB_RANTIME
"  100 AThe Giant Lemure sits motionlessly before you.@
   110 AThe Giant Lemure loses a rather large chunk of flesh.@
   120 AThe flesh slides back and rejoins the Lemure.@
   130 AYou notice several chunks of flesh slide through the entrance.@
   140 AThe chunks leap from the ground and mold to the Lemure's body.@
"

/* Dil to add extra fighting power */

dilcopy combat_mag@function("fireball", "", 25, 2);

end



                                isophacles
names {"isophacles", "demon"}
title "Isophacles the hunted"
descr
"Before you stands the hunted one, Isophacles."
extra {}
"The hunted one very closely resembles a human, in fact
it has been told that it once was a very low member of
that species. It spends its life now in the constant
terror of being hunted by none other than the king of
evil, Asmodeus. It will do so forever, never being caught
and living in a mortal nightmare where everything it encounters
is its enemy!."
level 40
alignment -600
flags {UNIT_FL_NO_TELEPORT}

race RACE_OTHER_UNDEAD
sex SEX_NEUTRAL
MSET_ABILITY (20,20,10,15,15,5,10,5)
MSET_WEAPON (5,5,0,45,0,0)
MSET_SPELL (5,5,0,0,0,0,35,0,0,0,0)
NATURAL_DEF(WPN_CLAW, ARM_HLEATHER)
weight 1175
money 5 SILVER_PIECE
special SFUN_RANDOM_ZONE_MOVE time 40 bits SFB_RANTIME
special SFUN_AGGRESSIVE time WAIT_SEC*4  bits SFB_RANTIME

dilcopy combat_mag@function("colourspray", "", 25, 2);

special SFUN_PAIN_INIT time WAIT_SEC*20 bits SFB_RANTIME
"  100 AIsophacles cowers in the shadows, almost in tears.@
   110 AThe hunted one cries 'Please, please, I am so scared'.@
   120 AIsophacles looks mad from his fears, he casts a cold eye at you.@
   130 AThe hunted one's eyes glow brightly and shine their glow at you!@
"
end

horseman1
names {"horseman of the apocolypse", "horseman"}
title "a horseman of the apocolypse"
descr "A Horseman of the Apocolypse stands before you."
extra {}
"This dreaded beast is part human and part equine and it
indeed looks most menacing to you. The human torso and head
are covered in a thick black mane, you notice dark red eyes
peering pack at you from under the thick hair. The equine
portion looks normal from what you can tell."
extra {"$status"}
"vacant"
extra {"$hunting"}
"noone"
race RACE_OTHER_UNDEAD
sex SEX_MALE
level 60
height 200
weight 2500
NATURAL_DEF(WPN_KICK, ARM_PLATE)
alignment -950
exp 130
MSET_ABILITY(12,12,12,12,12,12,12,16)
MSET_WEAPON(5,5,6,6,6,6)
MSET_SPELL(6,6,6,6,6,6,6,6,6,6,6)

dilcopy combat_mag@function("colourspray", "", 25, 2);


/* Dil to make extra strong in battle *

dilbegin prg29();
code
{
   :start:
   wait(SFB_COM, activator.type == UNIT_ST_PC);

   if (self.hp < self.max_hp)
   {
      self.hp := self.max_hp - (self.max_hp / 10);
   }
   goto start;
}
dilend

/* Dil to block all commands when 'held' */
dilbegin prg30();
code
{
   :start:
   wait(SFB_CMD, ((activator.type == UNIT_ST_PC) and
       (HELD in activator.quests) and (not(command(CMD_TELL))) and
       (not(command(CMD_SAY))) and (not(command(CMD_SHOUT))) and
       (not(command(CMD_LOOK)))));

   if (activator.alignment >= 100)
   {
      act("The Horseman bows as he releases his mental hold over you.",
	  A_ALWAYS, activator, null, null, TO_CHAR);
      act("The Horseman bows to "+activator.name+" as he releases his hold.",
          A_SOMEONE, activator, null, null, TO_REST);
      subextra(activator.quests, HELD);
      goto start;
   }

   block;
   act("The Horseman of the Apocolypse has gained control over your " +
	"mind, your attempts are futile.",
	A_ALWAYS, activator, null, null, TO_CHAR);
   act(activator.name+" attempts are thwarted by the Horseman's grasp.",
        A_SOMEONE, activator, null, null, TO_REST);
}
dilend

/* Dil to check for evil players */

dilbegin prg31();
var
	pc	:	unitptr;
	extra1	:	extraptr;
	num	:	integer;
code
{
   :start:
   wait(SFB_CMD, ((activator.type == UNIT_ST_PC) and
       (activator.level > 40) and (activator.level < 51) and
       (activator.alignment < -700)));

   extra1 := "$status" in self.extra;

   if (VACANT in extra1.descr) /* Set the mob to hunt */
   {
      subextra(self.extra, "$status");
      subextra(self.extra, "$hunting");
      addextra(self.extra, {"$status"}, "withprey");
      addextra(self.extra, {"$hunting"}, activator.name);
      goto start;
   }

   if ((extra1.descr $= "withprey") or (extra1.descr $= "preygone"))
   /* Already got one */
   {
      goto start;
   }
}
dilend

/* Dil to capture the player */
dilbegin prg32();
var
	pc	:	unitptr;
	extra1	:	extraptr;
	extra2	:	extraptr;
        rstring	:	string;
 	num	:	integer;
code
{
   :init:
   heartbeat := PULSE_SEC*5;

   :start:
   wait(SFB_CMD, ((activator.type == UNIT_ST_PC) and
       (activator.level > 40) and (activator.level < 51) and
       (activator.alignment < -700)));

   extra1 := "$status" in self.extra;
   extra2 := "$hunting" in self.extra;

   if (not("withprey" in extra1.descr)) goto start;
   if (not(activator.name in extra2.descr)) goto cleanup;

   pc := activator;
   secure(pc, lost_pc1);
   exec("sit", self);
   if (HELD in pc.quests)
   {
      exec("emote grins at "+pc.name+" and mumbles something softly.", self);
      pause;
      exec("say It appears we have met again eh?", self); pause;
      num := rnd(1, 99);
      if (num > pc.level-25) goto gotcha;
      else
      {
	 subextra(pc.quests, HELD);
	 goto no_gotcha;
      }
   }

   priority;
   act("The Horseman grins with a lifeless smile that makes you cringe. ",
	A_ALWAYS, pc, null, null, TO_CHAR);
   act("The Horseman stares coldly at "+pc.name+", your nerves tingle!",
	A_SOMEONE, pc, null, null, TO_REST);
   HOLD
   act("The clouds overhead darken dramatically, the ground beneath quivers.",
       A_SOMEONE, pc, null, null, TO_ALL);
   HOLD
   act("The Horseman stares at you, you feel his power invade your body.",
	A_ALWAYS, pc, null, null, TO_CHAR);
   act("You feel the Horseman's grim stare cast on "+pc.name+".",
	A_SOMEONE, pc, null, null, TO_REST);
   num := rnd(1,99);

   if (num > pc.level)
   {
      act("You feel yourself losing control of your mind and body, it seems " +
	  "all your attempts are thwarted.",
	  A_ALWAYS, pc, null, null, TO_CHAR);
      addextra(pc.quests, {HELD}, "");
      HOLD
      act("You grimly realize your soul is being controlled by the Horseman.",
	  A_ALWAYS, pc, null, null, TO_CHAR);
      act(pc.name+" drops to the ground, obviously in extreme pain.",
	  A_SOMEONE, pc, null, null, TO_REST);
      HOLD
      :gotcha:
      unsecure(pc);
      secure(pc, come_here1);
      rstring := pc.outside.zoneidx+"/"+pc.outside.nameidx;
      act(pc.name+" is dragged down beneath the surface by the Horseman." +
	  "By the time you realize what has happened it is too late.",
	  A_SOMEONE, pc, null, null, TO_REST);
      act("You have been dragged down beneath the surface by the " +
           "Horseman, the heat intensifies noticably as you descend." +
           " It appears the Horseman has left you here.",
           A_ALWAYS, pc, null, null, TO_CHAR);
      link(pc, findroom("under_ground1@hades5"));
   }else{
	:no_gotcha:
	act("You feel the Horseman's intellect working its way into your head.",
	    A_ALWAYS, pc, null, null, TO_CHAR);
	act(pc.name+" shouts in pain, fighting to keep control!",
	    A_SOMEONE, pc, null, null, TO_REST);
        HOLD
 	pc.hp := pc.hp - (pc.hp / 2);
        act("All at once the pain intensifies into a cacophony of exquisite " +
	    "agony and then vanishes.",
	    A_ALWAYS, pc, null, null, TO_CHAR);
        act("The mental anguish "+pc.name+" suffers is clearly visible now.",
            A_SOMEONE, pc, null, null, TO_REST);
        unsecure(pc);
	goto cleanup;
   }

   :come_here1:
   link(self, findroom("hades5/under_ground1"));
   exec("cackle", self); pause;
   exec("say Enjoy your stay mortal!", self);
   exec("emote is engrossed in a dark ominous cloud, together they " +
	 "vanish.", self);
   link(self, findroom(rstring));
   act("The Horseman emerges from the ground empty handed.",
	A_ALWAYS, self, null, null, TO_ALL);
   exec("stand", self);
   goto cleanup;

   :lost_pc1:
   exec("stand", self);
   goto start;

   :lost_pc:
   subextra(self.extra, "$status");
   addextra(self.extra, {"$status"}, "vacant");
   subextra(self.extra, "$hunting");
   addextra(self.extra, {"$hunting"}, "noone");
   nopriority;
   exec("stand", self);
   goto start;

   :cleanup:
   HOLD
   subextra(self.extra, "$status");
   subextra(self.extra, "$hunting");
   addextra(self.extra, {"$hunting"}, "noone");
   addextra(self.extra, {"$status"}, "vacant");
   nopriority;
   exec("stand", self);
   goto start;

}
dilend

special SFUN_PAIN_INIT time WAIT_SEC*25 bits SFB_RANTIME
"  100 AThe Horseman stares coldly at you through his dark mane.@
   110 AThe Horseman prepares to walk the land in search of evil.@
   115 Gmidgaard/cth_altar@
   120 AThe Horseman scans the room for the presence of evil.@
   125 Cemote shuts his eyes and begins to growl rathewr angrily.@
   130 Gudgaard/village_rd1@
   135 AThe Horseman looks about the room for evil, he glances at you!@
   140 Cemote growls loudly and begins to snort what looks like fire.@
   145 Gudgaard/square@
   150 Cemote mutters 'There is so much evil in the land, this will be easy.'@
   160 AThe Horseman snorts a blast of fire and takes in his surroundings.@
   170 Gmidgaard/market_sq@
   180 Cemote looks around for signs of an evil being, he must find one soon!@
   190 Csay I must bring in my quota or the evil lord will flay me alive!@
   200 Ghades1/dust_room@
   210 Cemote mutters 'My lord, I have returned to you!.'@
"
end






/* *********************** Objects Section ********************** */

%objects

/* ********************** Objects for Plane 1 ******************* */

                                 dev_trident

names {"devil trident","trident"}
title "a Devil's Trident"
descr
"A monsterous flaming trident lies here, scorching the ground."
extra {}
"You are surprised that the weapon doesn't burn your hands as you try to
wield it.  It is very impressive and quite heavy.  You feel as if with a
weapon you could take on the world, or underworld."
manipulate {MANIPULATE_TAKE, MANIPULATE_WIELD}
WEAPON_DEF(WPN_TRIDENT, +3, +3)
STR_TRANSFER(+1)
DEX_TRANSFER(+1)
DIV_TRANSFER(-1)
SPELL_TRANSFER(SPL_IDENTIFY_1, +1)
SPELL_TRANSFER(SPL_IDENTIFY_2, +2)
WEAPON_TRANSFER(WPN_TRIDENT, +2)
weight 12
cost 510 COPPER_PIECE
rent 255 IRON_PIECE
end


                                tr_scroll

names {"scroll of translation", "scroll", "translation scroll"}
title "a scroll of translation"
descr
"An ancient looking scroll lies here at your feet."
extra {}
"It looks to be several ceturies old but how could that be?
The parchment it is made from surprisingly seems sturdy enough
for you to not worry about its decay. You cannot understand a
word on it however, the language it is written in is nothing
like you have seen before."
manipulate {MANIPULATE_TAKE, MANIPULATE_HOLD}
type ITEM_TRASH
weight 1
cost 1 GOLD_PIECE
end



				sight_gem
names {"gem of sight", "sight gem", "gem"}
title "a gem of sight"
descr "A Gem of sight pulsates gently before you."
extra {}
"The Gem of sight burns most brightly when held and the command:
'see <playername>' is given. The bearer will see the surroundings
of the player (if present) based on the divine skill."
manipulate {MANIPULATE_TAKE, MANIPULATE_HOLD}
flags {UNIT_FL_MAGIC}
type ITEM_OTHER
cost 200 COPPER_PIECE
rent 100 IRON_PIECE
weight 2

/* Dil to give power of sight */

dilbegin prg33();
var
	pc		:	unitptr;
	vict		:	unitptr;
	room1		:	string;
	room2		:	string;
	num1		:	integer;
 	div_skill    	:	integer;
	arg		:	string;
code
{
   :init:
   heartbeat := PULSE_SEC*4;

   :start:
   wait(SFB_CMD, ((activator.type == UNIT_ST_PC) and
       (self.outside.name $= activator.name) and
       (self.equip == WEAR_HOLD) and (command("see"))));

   block;
   if (activator.level <= 25)
   {
      act("The Gem requires a level of 25 or higher to use.",
	  A_ALWAYS, activator, null, null, TO_CHAR);
      goto start;
   }

   if (activator.outside.type!=UNIT_ST_ROOM)
   {
	 act ("Your Gem of sight requires more room to work correctly.  Try the Gem "+
	 "while in a room.",
	  A_ALWAYS, activator, null, null, TO_CHAR);
      goto start;
   }


   arg := argument;
   if (not(arg))
   {
       act("To use the sight gem do 'see <playername>'.",
	   A_ALWAYS, activator, null, null, TO_CHAR);
       goto start;
   }

   vict := findunit(activator, arg, FIND_UNIT_GLOBAL, null);
   if (not(vict.type == UNIT_ST_PC))
   {
       act("That person could not be found.",
	   A_ALWAYS, activator, null, null, TO_CHAR);
       goto start;
   }

   if (vict.level > 50)
   {
	act("Gods do not like having their privacy invaded!",
	    A_ALWAYS, activator, null, null, TO_CHAR);
	goto start;
   }

   pc := activator;
   secure(pc, lost_pc);

   num1 := rnd(0, 99);
   div_skill := self.outside.abilities[ABIL_DIV];
   if ((div_skill*num1)/150 >= 20)
   {
      act("The Gem of sight shares its view of the world with you.",
          A_ALWAYS, pc, null, null,TO_CHAR);
      room1 := pc.outside.nameidx+"@"+pc.outside.zoneidx;
      room2 := vict.outside.nameidx+"@"+vict.outside.zoneidx;
      link(pc, findroom(room2));
      exec("look", pc);
      link(pc, findroom(room1));
   }
   else
     act("The Gem cannot utilize your Divine essence sufficiently.",
         A_ALWAYS, pc, null, null,TO_CHAR);

   unsecure(pc);

   :lost_pc:
   goto start;
}
dilend

end

                                gray_gem
names {"gray gem", "gem"}
title "a Gray Gem"
descr "A dull Gray Gem is here."
extra {}
"It seems like an ordinary stone, maybe less than ordinary by the
looks of the cut. You can tell this stone was never set in any sort
of surface, it must not have been intended for decoration."
extra {"$iden~tify"}
"When held this gem will boost the wearer's strength."
manipulate {MANIPULATE_TAKE, MANIPULATE_HOLD}
flags {UNIT_FL_MAGIC}
type ITEM_TREASURE
STR_TRANSFER(+1)
weight 2
cost 150 COPPER_PIECE
rent 75 IRON_PIECE

special SFUN_RESTRICT_OBJ "Level = 35"

/* Dil to show player the gem is sacred */
dilbegin prg34();
code
{
   :init:
   heartbeat := PULSE_SEC*4;

   :start:
   wait(SFB_CMD, command(CMD_HOLD));

   if ((self.outside.name $= activator.name) and
      ("gray gem" in argument))
   {
      act("The Gray Gem glows brightly, you know it is Sacred!",
          A_ALWAYS, activator, null, null, TO_CHAR);
      act("The Gray Gem glows brightly in "+activator.name+"'s hand.",
          A_SOMEONE, activator, null, null, TO_REST);
   }
}
dilend

end

                                gold_gem
names {"gold gem", "gem"}
title "a Gold Gem"
descr "A shiny Gold Gem is here."
extra {}
"It seems like an ordinary stone, maybe less than ordinary by the
looks of the cut. You can tell this stone was never set in any sort
of surface, it must not have been intended for decoration. There is
a scuff along the side greatly reducing the beauty of this stone."
manipulate {MANIPULATE_TAKE, MANIPULATE_HOLD}
flags {UNIT_FL_MAGIC}
type ITEM_TREASURE
STR_TRANSFER(-1)
CON_TRANSFER(-1)
weight 1
cost 6 SILVER_PIECE
rent 3 COPPER_PIECE
end

                                green_gem
names {"green gem", "gem"}
title "a Green Gem"
descr "A small Green Gem is here."
extra {}
"It seems like an ordinary stone, maybe less than ordinary by the
looks of the cut. You can tell this stone was never set in any sort
of surface, it must not have been intended for decoration. Strangely
enough you could swear there are tooth marks in the surface of the
stone."
manipulate {MANIPULATE_TAKE, MANIPULATE_HOLD}
flags {UNIT_FL_MAGIC}
type ITEM_TREASURE
DEX_TRANSFER(-1)
CHA_TRANSFER(-1)
weight 1
cost 6 SILVER_PIECE
rent 3 COPPER_PIECE
end

/* ************************* Reset Section ************************* */

%reset

/* ********************** Door Reset Section *********************** */

door ent_hell NORTH {EX_OPEN_CLOSE, EX_CLOSED}
door marble_court3 NORTH {EX_OPEN_CLOSE, EX_CLOSED, EX_HIDDEN}
door rope_bridge SOUTH {EX_OPEN_CLOSE, EX_CLOSED}

/* ********************* Reset for Plane 1 ************************ */

load erinyes into lit_cavern2 max 6 local 1
load erinyes into lit_cavern3 max 6 local 1
load erinyes into lit_cavern4 max 6 local 1

load isophacles into plane1_rm1 max 1
{
	load sight_gem max 15
}
load lemure_type1 into plane1_rm4 max 6 local 1
load lemure_type1 into plane1_rm5 max 6 local 1
load lemure_type1 into plane1_rm9 max 6 local 1
load lemure_type1 into plane1_rm11 max 6 local 1
load lemure_type2 into marble_court1 max 10 local 1
load giant_lemure into large_cavern max 1
{
        equip gray_gem position WEAR_HOLD
}

/* room doesn't exist and Zandar is tired of seeing the log error :) */
/* load horseman1 into dust_room max 1 */

%end



